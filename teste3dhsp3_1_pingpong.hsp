
//３Ｄモデルに、ひとつだけモーションを読み込み、再生するサンプルです。

;	e3dhsp.dll test script

#include "e3dhsp3.as"


#module
#deffunc waitbyfps int p1, var fps
	E3DWaitbyFPS@ p1, fps
	await 0
	return
#global
	


	dim keybuf, 256
	

	;file のpath 用のバッファーの作成
	pathlen = 2048
	sdim mediadir, pathlen
	mediadir = dir_cur + "\\Media"
	sdim pathbuf, pathlen, 2


	screenw = 640 : screenh = 480
	screen 0, screenw, screenh, 1

	title "Easy3D for HSP3 sample"

	;初期化
	E3DEnableDbgFile
	E3DInit 0, -1, 0, 16, 0, scid1

	;カメラの初期化
	camposx = 0.0 : camposy = 800.0 : camposz = -2500.0
	E3DSetCameraPos camposx, camposy, camposz	
	camdegxz = 180.0 : camdegy = 0.0
	E3DSetCameraDeg camdegxz, camdegy

	;projectionの変更をしたいときは、以下の２行を有効にしてください。
	proj_near = 100.0 : proj_far = 30000.0 : proj_fov = 60.0
	E3DSetProjection proj_near, proj_far, proj_fov


	//猫データ読み込み
	pathbuf.0 = mediadir + "\\hayame.sig"
	E3DSigLoad pathbuf.0, hsid1
	E3DSetPos hsid1, 1000.0, 0.0, 0.0

	//モーションの読み込み
	pathbuf.0 = mediadir + "\\test_hayame_boot_pp.qua"
	E3DAddMotion hsid1, pathbuf.0, motid1, framemax1
	E3DSetMotionKind hsid1, motid1 	


	//猫データ読み込み
	pathbuf.0 = mediadir + "\\hayame.sig"
	E3DSigLoad pathbuf.0, hsid2
	E3DSetPos hsid2, -1000.0, 0.0, 0.0

	//モーションの読み込み
	pathbuf.0 = mediadir + "\\test_hayame_boot_pp.qua"
	E3DAddMotion hsid2, pathbuf.0, motid2, framemax2
;	E3DSetMotionKind hsid2, motid2 	
	E3DSetMotionFrameNo hsid2, motid2, 180



	//ライトの作成
	lightdirx1 = 0.0
	lightdiry1 = 0.0
	lightdirz1 = 1.0
	lightr1 = 255
	lightg1 = 255
	lightb1 = 255

	E3DCreateLight lid1
	E3DSetDirectionalLight lid1, lightdirx1, lightdiry1, lightdirz1, lightr1, lightg1, lightb1

	chkfps1 = 0

*main
	; keybuf変数は、メインループの外で、dim keybuf, 256 として、確保しておいてください。
	E3DGetKeyboardState keybuf

	if keybuf.VK_ESCAPE = 1 : goto *bye ; [ESC]で終了

	E3DSetNewPose hsid1, frameno1
	E3DSetNewPose hsid2, frameno2

	E3DGetMotionFrameNo hsid1, curmk1, curframe1
	E3DGetMotionFrameNo hsid2, curmk2, curframe2


	E3DBeginScene scid1
		E3DChkInView scid1, hsid1
		E3DChkInView scid1, hsid2

		;不透明部分の描画をする
		E3DRender scid1, hsid1, 0, frameno1, 0
		E3DRender scid1, hsid2, 0, frameno2, 0

		;半透明部分の描画をする
		E3DRender scid1, hsid1, 1, frameno1, 0, 0, 0, 1
		E3DRender scid1, hsid2, 1, frameno2, 0, 0, 0, 1

		gosub *DrawText

	E3DEndScene
	E3DPresent scid1
	
	waitbyfps 60, chkfps1

	goto *main


*bye
	E3DBye
	end

*DrawText
	textr = 255 : textg = 255 : textb = 255
	textscale = 1
	
	textposx = 10 : textposy = 0
	strchk0 = "curmk1 " + curmk1 + " curframe1 " + curframe1
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk0

	textposy += 20
	strchk0 = "curmk2 " + curmk2 + " curframe2 " + curframe2
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk0
	

	return
	


