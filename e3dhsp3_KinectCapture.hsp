
//３Ｄモデルに、ひとつだけモーションを読み込み、再生するサンプルです。

;	e3dhsp.dll test script

#include "e3dhsp3.as"


#module
#deffunc waitbyfps int p1, var fps
	await 0
	E3DWaitbyFPS@ p1, fps
	return
#global
	

	dim arfps, 50
	dim keybuf, 256
	
	;file のpath 用のバッファーの作成
	pathlen = 2048
	sdim mediadir, pathlen
	mediadir = dir_cur + "\\Media"
	sdim pathbuf, pathlen, 2

	screenw = 640 : screenh = 480
	screen 0, screenw, screenh, 1
	title "Easy3D for HSP3 sample"

	;初期化
	E3DEnableDbgFile
	E3DInit 0, -1, 0, 16, 0, scid1

	;カメラの初期化
	camposx = 0.0 : camposy = 800.0 : camposz = -2800.0
	E3DSetCameraPos camposx, camposy, camposz	
	camdegxz = 180.0 : camdegy = 0.0
	E3DSetCameraDeg camdegxz, camdegy

	;projectionの変更をしたいときは、以下の２行を有効にしてください。
	proj_near = 1000.0 : proj_far = 30000.0 : proj_fov = 45.0
	E3DSetProjection proj_near, proj_far, proj_fov


//	pathbuf.0 = mediadir + "\\KinectSample\\my\\my9yoko.sig"
	pathbuf.0 = mediadir + "\\KinectSample\\bonnya\\bonnya_cap4.sig"
	E3DSigLoad pathbuf.0, hsid1
	E3DSetPos hsid1, 0.0, 0.0, 0.0


//モーションの読み込み
//	pathbuf.0 = mediadir + "\\KinectSample\\my\\empty800.qua"
	pathbuf.0 = mediadir + "\\KinectSample\\bonnya\\empty800.qua"
	E3DAddMotion hsid1, pathbuf.0, motid1, framemax1

	E3DSetMotionKind hsid1, motid1 	

	//ライトの作成
	lightdirx1 = 0.0
	lightdiry1 = 0.0
	lightdirz1 = 1.0
	lightr1 = 255
	lightg1 = 255
	lightb1 = 255

	E3DCreateLight lid1
	E3DSetDirectionalLight lid1, lightdirx1, lightdiry1, lightdirz1, lightr1, lightg1, lightb1

	chkfps1 = 0

//kinect
	E3DKinectLoadLib validflag
	if( validflag == 0 ){
		dialog "kinectのライブラリ読み込みに失敗しました。終了します。"
		end
	}

//	E3DKinectSetChara hsid1, mediadir + "\\KinectSample\\my\\ConvSetting3.kst", motid1, CAPMODE_ONE
	E3DKinectSetChara hsid1, mediadir + "\\KinectSample\\bonnya\\ConvSetting3.kst", motid1, CAPMODE_ONE

	E3DKinectStartCapture//10秒ほど掛かります

	ddim tpos, 4
	tpos(0) = 0.1
	tpos(1) =  0.1
	tpos(2) = -1.0
	tpos(3) = 1.0

	ddim skelpos, 3, SKEL_MAX

*main
	; keybuf変数は、メインループの外で、dim keybuf, 256 として、確保しておいてください。
	E3DGetKeyboardState keybuf

	if( keybuf.VK_ESCAPE = 1 ){
		goto *bye ; [ESC]で終了
	}

	E3DKinectGetSkelPos skelpos, getflag

	E3DKinectIsTracking trackflag, capcnt
	if( (getflag == 1) && (trackflag == 1) ){
//		E3DKinectSetSkelPos 1, POSMODE_TOPOFJOINT, skelpos, frameno1
//		E3DKinectSetSkelPos 1, POSMODE_ZERO, skelpos, frameno1
		E3DKinectSetSkelPos 1, POSMODE_GLOBAL, skelpos, frameno1		
	}
	
//	E3DSetNewPose hsid1, frameno1

	E3DBeginScene scid1
		E3DChkInView scid1, hsid1

		;不透明部分の描画をする
		E3DRender scid1, hsid1, 0, frameno1, 0
		;半透明部分の描画をする
		E3DRender scid1, hsid1, 1, frameno1, 0, 0, 0, 1

		E3DKinectRenderSilhouette 1, tpos

		gosub *DrawText
		
	E3DEndScene
	E3DPresent scid1
	
	waitbyfps 30, chkfps1
	arno = 0
	repeat 49
		befno = 48 - arno
		aftno = 49 - arno
		arfps.aftno = arfps.befno
		arno++
	loop	
	arfps.0 = chkfps1


	goto *main


*bye
	E3DKinectEndCapture//これをしないと終了に時間が掛かります。
	E3DBye
	end

*DrawText
	textr = 255 : textg = 255 : textb = 255
	textscale = 1

	lcnt = 0
	avfps = 0
	repeat 50
		avfps += arfps.lcnt
		lcnt++
	loop
	avfps = avfps / 50
	textposx = 10 : textposy = 80
	strchk3 = "avfps   " + avfps
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk3


	E3DGetPos hsid1, chkx, chky, chkz
	textposx = 10 : textposy = 140
	strchk3 = "pos " + chkx + "  " + chky + "  " + chkz
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk3

	return


