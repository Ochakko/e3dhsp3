	
	#include "e3dhsp3.as"

	renderflag = -1		;この変数が０の時は描画をスキップ。どこからでも参照出来るようにグローバル。

	#define global ENSkipE3DBeginScene							if renderflag@ : E3DBeginScene
	#define global ENSkipE3DEndScene							if renderflag@ : E3DEndScene
	#define global ENSkipE3DPresent(%1)							if renderflag@ : E3DPresent %1
	#define global ENSkipE3DRender(%1,%2,%3,%4,%5,%6,%7,%8)		if renderflag@ : E3DRender %1,%2,%3,%4,%5,%6,%7,%8
	#define global ENSkipE3DRenderBillboard (%1)				if renderflag@ : E3DRenderBillboard  %1
	
#module "ENSkipPlus"
;フレームスキップ用ウェイト

	#const	SKIPFRAMEMAX	4	;連続で飛ばすフレームの最大-1。fps〜(fps/SKIPFRAMEMAX)となる。

	#uselib "kernel32"
	#func QueryPFreq "QueryPerformanceFrequency" var
	#func QueryPCount "QueryPerformanceCounter" var
	#func Sleep "Sleep" int

#deffunc waitbyfpsInit
	;最初に一回だけ呼ぶ
	QueryPFreq nFreq	;1秒あたりカウント数。たいてい3579545
	if nFreq{
		skipframe = 0
		renderflag@ = -1		;
		QueryPCount precount	;とりあえずカウンタ初期化
		nextcount = precount
	}
	else{
		; QueryPerformanceCounterが無い（そんな事はまずありませんが）
		dialog "not ready QueryPerformanceCounter"
		end
	}

	return

#deffunc waitbyfpsskip int fps, var chkfps

	await 0	;入れとかないとキー入力を受け付けなくなるので
	if fps > 0{;一応
		countperframe = nFreq / fps
		QueryPCount precount
		if (precount > nextcount) {;過ぎてんじゃん！（あるいはオーバーフロー）
			if skipframe < SKIPFRAMEMAX{;フレームスキップ
				renderflag@ = 0
				skipframe ++
			}
			else{	;飛ばし過ぎたので強制描画
				renderflag@ = -1
				skipframe = 0
				nextcount = precount + 1	;仕方が無いのでカウンタもリセット（早送りになってしまうので）
			}
		}
		else{
			skipframe = 0
			renderflag@ = -1
			;QueryPCount precount
			msperframe = 1000 / fps
			sleeptime = limit(900 * (nextcount - precount) / nFreq, 1, msperframe)
			if sleeptime < 0 : dialog "sleeptime "+sleeptime;セーフティ
			sleep sleeptime	; QueryPerformanceCounterをmsに（1000→900にしてちょっと少なめに）
			repeat msperframe	;微調整はループで
				QueryPCount precount
				if precount > nextcount : break
				await 0
				;sleep 1	;awaitでテスト
			loop

		}
		nextcount += countperframe
		if (nextcount - precount) : chkfps = nFreq / (nextcount - precount)
	}
	return

#global

	waitbyfpsInit

	#const	LODNUM		50	;本数
	#const	AROUNDSTEP	8	;何方向から見るか。多いほど細かい
	#const	TXHEIGHT	128	;作成するビルボードの解像度（高さ）

	#enum	LOD_DIR	= 0
	#enum	LOD_STAT	;0:ビルボード
	#enum	LOD_SIGID
	#enum	LOD_BBID
	#enum	LOD_QID
	#enum	LOD_POSX
	#enum	LOD_POSY
	#enum	LOD_POSZ
	#enum	LOD_PARAM
	
	dim lod, LOD_PARAM, LODNUM
	dim bbtxid, AROUNDSTEP
	dim bb_scid, AROUNDSTEP
	
	dim keybuf, 256	

	;file のpath 用のバッファーの作成
	pathlen = 2048
	sdim mediadir, pathlen
	mediadir = dir_cur + "\\Media"
	sdim pathbuf, pathlen, 4
	

	mapsize = 150000.0
	mapdiv = 50

	mapheight = 100000.0

	screenw = 640 : screenh = 480
	screen 0, screenw, screenh, 1
	pointmax = 100

	title "遠景BB化"

	;初期化
	E3DInit 0, -1, 0, 32, 0, scid1

	;カメラの初期化
	camposx = 0.0 : camposy = 2500.0 : camposz = 0.0
	E3DSetCameraPos camposx, camposy, camposz	
	camdegxz = 180.0 : camdegy = 0.0
	E3DSetCameraDeg camdegxz, camdegy

	E3DCreateQ camq
	E3DCreateQ invcamq

	confflag = 0
	endflag = 0


	;projectionの変更をしたいときは、以下の２行を有効にしてください。
	proj_near = 100.0 : proj_far = 50000.0 : proj_fov = 60.0
	E3DSetProjection proj_near, proj_far, proj_fov

	E3DCreateLight lid1

	E3DSetLightAmbient lid1, 77, 77, 77
	E3DSetLightSpecular lid1, 0, 0, 0

	E3DCreateProgressBar
	E3DSetProgressBar 30

	
	; 地面データのロード

	pathbuf.0 = mediadir + "\\test_course_01.mqo"
	E3DLoadMQOFileAsGround pathbuf.0, 100.0, hsid0
	E3DSetPos hsid0, 0.0, 0.0, 0.0	
	frameno0 = 0

	E3DSetProgressBar 60

	; たこの読み込み	
	pathbuf.0 = mediadir + "\\tako.sig"
	pathbuf.1 = mediadir + "\\tako_idling.qua"
	pathbuf.2 = mediadir + "\\tako_run.qua"
	E3DSigLoad pathbuf.0, hsid4
	E3DAddMotion hsid4, pathbuf.1, mk4_idling, max4_idling				
	E3DAddMotion hsid4, pathbuf.2, mk4_run, max4_run

	E3DSetMotionKind hsid4, mk4_run

	posx4 = 117424.0 : posy4 = 7000.0 : posz4 = 44745.0	
	degx4 = 0.0 : degy4 = 162.0 : degz4 = 0.0
	E3DSetPos hsid4, posx4, posy4, posz4
	E3DSetDir hsid4, degx4, degy4, degz4
	frameno4 = 0
	newmk4 = 0
	curposx4 = posx4 : curposy4 = posy4 : curposz4 = posz4
	E3DSetBeforePos hsid4

	;準備
	pathbuf = mediadir + "\\bone_tree.sig"
	E3DSigLoad pathbuf.0, treehsid

	/*
	E3DGetBBox treehsid, -1, 0, minx, miny, minz, maxx, maxy, maxz;うまく機能してない様子
	maxx = 750.0
	maxy = 1500.0
	maxz = 750.0
	minx = -750.0
	minx = 0.0
	minz = -750.0
	*/
	E3DGetDispObjNum treehsid, objectnum

	sdim objectname, 256, objectnum
	dim objectinfo, DOI_MAX, objectnum
	dim objectvertnum, objectnum
	dim objectfacenum, objectnum

	E3DGetDispObjInfo@ treehsid, objectnum, objectname, objectinfo, objectgetnum
	
	repeat objectnum
		E3DGetVertNumOfPart@ treehsid, objectinfo.DOI_SERIAL.cnt, objectvertnum.cnt
		if vertnummax < objectvertnum.cnt : vertnummax = objectvertnum.cnt
	loop
	
	repeat objectnum
		curobj = cnt
		repeat objectvertnum.curobj
			E3DGetVertPos@ treehsid, objectinfo.DOI_SERIAL.curobj, cnt, vertx, verty, vertz
			if minx > vertx : minx = vertx
			if miny > verty : miny = verty
			if minz > vertz : minz = vertz
			if maxx < vertx : maxx = vertx
			if maxy < verty : maxy = verty
			if maxz < vertz : maxz = vertz
		loop
	loop
	
	bbsizew = abs(maxx)
	if bbsizew < abs(minx) : bbsizew = abs(minx)
	if bbsizew < abs(maxz) : bbsizew = abs(maxz)
	if bbsizew < abs(minz) : bbsizew = abs(minz)
	bbsizew *= 2.0	;絶対値の最大を2倍
	bbsizeh = maxy	;0原点なのでこれでよしとする
	;dialog "bbsizew"+bbsizew+" bbsizeh"+bbsizeh
	
	camposx = 0.0
	camposy = bbsizeh / 2
	camposz = 0.5 * bbsizeh / tan(3.141592 * proj_fov / 360)	

	E3DSetCameraPos camposx, camposy, camposz
	E3DSetCameraTarget 0, camposy, 0, 0, 1, 0
	E3DSetDirectionalLight lid1, 0, 0, -1, 255, 255, 255

	repeat AROUNDSTEP
		E3DCreateRenderTargetTexture TXHEIGHT * bbsizew / bbsizeh, TXHEIGHT, bb_scid.cnt, bbtxid.cnt, okflag
		if( okflag == 0 ){
			dialog "レンダーターゲットテクスチャが作成できません。\nプログラムを終了します。"
			goto *bye
		}
		E3DSetDir treehsid, 0, 360.0 * cnt / AROUNDSTEP, 0

		E3DBeginScene bb_scid.cnt
		
		E3DChkInView bb_scid.cnt, treehsid
		;不透明部分の描画をする
		E3DRender bb_scid.cnt, treehsid, 0, 0, 0
		;半透明部分の描画をする
		E3DRender bb_scid.cnt, treehsid, 1, 0, 0, 0, 0, 1
		
		E3DEndScene
	loop

	;E3DEnableTexture treehsid, -1, 0

	E3DSeed 321
	E3DChkInView scid1, hsid0
	repeat LODNUM
		lod.LOD_SIGID.cnt = treehsid
		E3DCreateBillboard "", bbsizew, bbsizeh, 0, lod.LOD_BBID.cnt
		E3DCreateQ lod.LOD_QID.cnt

		divsize = mapsize / 40

		E3DRand divsize, rand1
		E3DRand divsize, rand2
		
		lod.LOD_POSX.cnt = rand1 * 40
		lod.LOD_POSZ.cnt = rand2 * 40

		;E3DSetBillboardOnGround lod.LOD_BBID.cnt, hsid0, mapheight, -100, lod.LOD_POSX.cnt, lod.LOD_POSZ.cnt
		E3DChkConfGround2 lod.LOD_POSX.cnt, mapheight, lod.LOD_POSZ.cnt, lod.LOD_POSX.cnt, -100, lod.LOD_POSZ.cnt, hsid0, 0, 0, -100, result, adjustx, adjusty, adjustz, nx, ny, nz
		lod.LOD_POSY.cnt = int(adjusty)
		E3DSetBillboardPos lod.LOD_BBID.cnt, lod.LOD_POSX.cnt, lod.LOD_POSY.cnt, lod.LOD_POSZ.cnt
		
		E3DRand AROUNDSTEP, lod.LOD_DIR.cnt
		
		E3DSetTextureToDispObj -1, lod.LOD_BBID.cnt, bbtxid(lod.LOD_DIR.cnt)
	loop        

	lightdirx1 = 0.0
	lightdiry1 = -1.0
	lightdirz1 = 2.0
	lightr1 = 255
	lightg1 = 255
	lightb1 = 255

	E3DSetDirectionalLight lid1, lightdirx1, lightdiry1, lightdirz1, lightr1, lightg1, lightb1
	E3DSetLinearFogParams 1, 96, 96, 128, proj_near, proj_far


;/////////////////	
	
	testfps = 0



	chkfps1 = 0

	pathbuf.0 = mediadir + "\\lake.bmp"
	pathbuf.1 = mediadir + "\\cloud3.bmp"
	E3DCreateBG scid1, pathbuf.0, pathbuf.1, 0.001, 0.0, 0

	E3DDestroyProgressBar

	pbflag = 0;

	result0 = 0 : result1 = 0 : result2 = 0 : result3 = 0
	adjustx0 = 0.0 : adjusty0 = 0.0 : adjustz0 = 0.0
	adjustx1 = 0.0 : adjusty1 = 0.0 : adjustz1 = 0.0
	adjustx2 = 0.0 : adjusty2 = 0.0 : adjustz2 = 0.0
	adjustx3 = 0.0 : adjusty3 = 0.0 : adjustz3 = 0.0

	befposx = posx4 : befposy = posy4 : befposz = posz4

	ccw = 0
	newdeg = 0.0
	nx1 = 0.0 : ny1 = 0.0 : nz1 = 0.0
	dot1 = 0.0 : dot2 = 0.0 : dot = 0.0

	dim arfps, 50

	maxfps = 60
	befshiftF = 0
	curshiftF = 0

	turboflag = 0

	targetpid4 = 0
	movestep = 200

	E3DEnableDbgFile

*main
	; keybuf変数は、メインループの外で、dim keybuf, 256 として、確保しておいてください。
	E3DGetKeyboardState keybuf

	if keybuf.VK_ESCAPE = 1 : goto *bye ; [ESC]で終了

	gosub *CheckTurbo
	gosub *ChangeFPS

	gosub *MoveChara2
	gosub *MoveCamera2


	E3DSetNewPose hsid4, frameno4

	;E3DQtoEuler camq, camdirx, camdiry, camdirz, 0, 0, 0
	E3DAtan2 (camposx - camtargetx), (camposz - camtargetz), camdiry
	if camdiry < 0 : camdiry += 360.0
	
	repeat LODNUM
		;E3DSetValidFlag@ -1, lod.LOD_BBID.cnt, 1	;Z値取得のため一時的にオン
		;E3DGetScreenPos3 scid1, -1, lod.LOD_BBID.cnt, scx, scy, scz, 0, 1	;これ機能してない様子


//		E3DGetScreenPos3 scid1, -1, lod.LOD_BBID.cnt, tscx, tscy, tscz, 0, 1
//		strchk = "scpos3 : " + cnt + " : " + tscx + "  " + tscy + "  " + tscz
//		E3DDbgOut strchk

		E3DVec3Length lod.LOD_POSX.cnt - backposx, lod.LOD_POSY.cnt - backposy, lod.LOD_POSZ.cnt - backposz, leng
		scz = leng / (proj_far - proj_near)	;仕方ないのでカメラからの距離で代替

		switch lod.LOD_STAT.cnt
		case 0	;現在遠距離
			if scz < 0.4{
				lod.LOD_STAT.cnt = 1	;sigに切り替え
				E3DSetValidFlag@ -1, lod.LOD_BBID.cnt, 0
				next_dir = int(-360.0 * lod.LOD_DIR.cnt / AROUNDSTEP - camdiry) 
				E3DSetQAxisAndDeg lod.LOD_QID.cnt, 0, 1, 0, -next_dir
				E3DDbgOut "bbid:"+cnt+" LOD_DIR:"+lod.LOD_DIR.cnt
			}
		swbreak
		case 1	;現在近距離
			if scz > 0.6{
				E3DQtoEuler lod.LOD_QID.cnt, dirx, diry, dirz, 0, 0, 0;たまに不自然な数値が出る
				tmp_dir = int(diry - camdiry) \ 360
				if tmp_dir < 0 :tmp_dir += 360
				lod.LOD_DIR.cnt = AROUNDSTEP * tmp_dir / 360
				E3DDbgOut "bbid:"+cnt+" dirx"+dirx+" diry"+diry+" dirz"+dirz+" (camdiry)"+camdiry+" LOD_DIR:"+lod.LOD_DIR.cnt+"("+tmp_dir
				E3DSetTextureToDispObj -1, lod.LOD_BBID.cnt, bbtxid(lod.LOD_DIR.cnt)
				lod.LOD_STAT.cnt = 0
				E3DSetValidFlag@ -1, lod.LOD_BBID.cnt, 1
			}
		swbreak
		swend
	loop

	E3DBeginScene scid1
	

	E3DChkInView scid1, hsid0
	E3DChkInView scid1, hsid4

	gosub *ChkConf ; !!! E3DChkInViewより後で呼んでください。


		;不透明部分の描画をする
	E3DRender scid1, hsid0, 0, frameno0, 0
	E3DRender scid1, hsid4, 0, frameno4, 0

	E3DRenderBillboard scid1

		;半透明部分の描画をする
	E3DRender scid1, hsid0, 1, frameno0, 0, 0, 0, 1
	E3DRender scid1, hsid4, 1, frameno4, 0, 0, 0, 1

	repeat LODNUM
		if lod.LOD_STAT.cnt{
			E3DSetPos treehsid, lod.LOD_POSX.cnt, lod.LOD_POSY.cnt, lod.LOD_POSZ.cnt
			E3DSetDirQ2@ treehsid, lod.LOD_QID.cnt
			E3DChkInView scid1, hsid0
			E3DRender scid1, treehsid, 0, 0, 0
			E3DRender scid1, treehsid, 1, 0, 0, 0, 0, 1
		}
	loop

	gosub *DrawText



//	repeat LODNUM
//		E3DGetScreenPos3 scid1, -1, lod.LOD_BBID.cnt, tscx, tscy, tscz, 0, 1
//		//E3DGetScreenPos3 scid1, -1, lod.LOD_BBID.cnt, tscx, tscy, tscz, -1, 1
//		strchk = "scpos3 : " + cnt + " : " + tscx + "  " + tscy + "  " + tscz
//		E3DDbgOut strchk
//	loop


	E3DEndScene

	E3DPresent scid1
		
	waitbyfpsskip maxfps, chkfps1

	arno = 0
	repeat 49
		befno = 48 - arno
		aftno = 49 - arno
		arfps.aftno = arfps.befno
		arno++
	loop	
	arfps.0 = chkfps1

	
	E3DGetPos hsid4, posx4, posy4, posz4
	E3DSetBeforePos hsid4
	befposx = posx4 : befposy = posy4 : befposz = posz4
	
	goto *main


*bye
	E3DDestroyBG

	E3DDestroyLight lid1

	E3DBye
	end

*MoveChara2
	forwardstep = 200.0
	backstep = -200.0
	degstep = 5.0
	mdegstep = -degstep

	posstep = 100.0

		
	; VK_　が頭につく、定数の詳細は、e3dhsp_vk.as をご覧ください。
	; keybuf は、GetKeyboardState 命令で、セットされます。

	if keybuf.VK_UP = 1 : E3DPosForward hsid4, forwardstep ;矢印上
	if keybuf.VK_DOWN = 1 : E3DPosForward hsid4, backstep ;矢印下
	if keybuf.VK_LEFT = 1 : E3DRotateY hsid4, mdegstep ;矢印左
	if keybuf.VK_RIGHT = 1 : E3DRotateY hsid4, degstep ;矢印右


	; a が　押されたときに、前進したいときは、下の様に記述します。
	;if keybuf.'A' = 1 : E3DPosForward hsid4, forwardstep

	return

*ChkConf
	result0 = 0

	; 地面その１と、キャラクタのあたり判定	
	E3DChkConfGround hsid4, hsid0, 1, 200.0, -100.0, result0, adjustx0, adjusty0, adjustz0, nx0, ny0, nz0
	if ( result0 != 0 ) {
		posx4 = adjustx0 : posy4 = adjusty0 : posz4 = adjustz0
		E3DSetPos hsid4, posx4, posy4, posz4		
	}	

	return


*MoveCamera2
	E3DGetPos hsid4, saveposx4, saveposy4, saveposz4
	E3DPosForward hsid4, -2500.0
	E3DGetPos hsid4, backposx, backposy, backposz
	E3DSetPos hsid4, saveposx4, saveposy4, saveposz4

	;E3DSetCameraPos backposx, backposy + 1000, backposz

	camposx = backposx
	camposy = backposy + 1500.0
	camposz = backposz
	E3DSetCameraPos camposx, camposy, camposz
	E3DSetCameraTarget saveposx4, saveposy4 + 700.0, saveposz4, 0.0, 1.0, 0.0

	camtargetx = saveposx4
	camtargetz = saveposz4
	
	E3DGetCameraQ@ camq@
	E3DInvQ@ camq@, invcamq@

	return


*CheckTurbo
	
	if ( keybuf.'T' ) {
	;;	E3DSetMotionStep hsid4, mk4_idling, 2
	;	E3DSetMotionStep hsid4, mk4_run, 2		
	;	movestep = 400.0
		blurcount ++
	}
	else {
	;	E3DSetMotionStep hsid4, mk4_idling, 1
	;	E3DSetMotionStep hsid4, mk4_run, 1		
	;	movestep = 200.0
		blurcount --
	}
	blurcount = limit(blurcount, 1, BLURNUM * 5)
	stick a, 0, 1
	if a & 1024 {;tab
		blurdrawflag ^= -1
	}

	return

*ChangeFPS

	curshiftF = 0
	if ( (keybuf.VK_SHIFT = 1) & (keybuf.'F' = 1) ) {
		curshiftF = 1
	}

	if ( ( curshiftF = 0 ) & ( befshiftF = 1 ) ) {
		if ( maxfps = 60 ) {
			maxfps = 500
		}
		else {
			maxfps = 60
		} 
	}

	befshiftF = curshiftF

	return

*DrawText

	textr = 0 : textg = 255 : textb = 0
	textscale = 1

	lcnt = 0
	avfps = 0
	repeat 50
		avfps += arfps.lcnt
		lcnt++
	loop
	avfps = avfps / 50

	textposx = 10 : textposy = 20
	strchk2 = "avfps   " + avfps
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk2

	textposy += 20
	strchk3 = "camdiry  : " + camdiry
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk3.0
	textposy += 20
	strchk3 = "camposx  : " + camposx + "camposz  : " + camposz
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk3.0
	textposy += 20
	strchk3 = "camtargetx  : " + camtargetx + "camtargetz  : " + camtargetz
	E3DDrawText textposx, textposy, textscale, textr, textg, textb, strchk3.0
	
	return
