
;	e3dhsp.dll test script

#include "e3dhsp3.as"


#module
#deffunc waitbyfps int p1, var fps
	E3DWaitbyFPS@ p1, fps
	await 0
	return
#global
	

	dim arfps, 50

	dim keybuf, 256
	

	;file のpath 用のバッファーの作成
	pathlen = 2048
	sdim mediadir, pathlen
	mediadir = dir_cur + "\\Media"
	sdim pathbuf, pathlen, 2


	screenw = 640 : screenh = 480
	screen 0, screenw, screenh, 1

	title "Easy3D for HSP3 sample"

	;初期化
	E3DEnableDbgFile
	E3DInit 0, -1, 0, 16, 0, scid1


	//地面読み込み
	pathbuf.0 = mediadir + "\\ground05mv.sig"
	E3DLoadSigFileAsGround pathbuf.0, hsid0, 0, 1.0

	//キャラクター
	pathbuf.0 = mediadir + "\\my\\my.sig"
	E3DSigLoad pathbuf.0, hsid1, 0, 3.0//３倍

	//moa読み込み
	pathbuf.0 = mediadir + "\\my\\my3.moa"
	E3DLoadMOAFile hsid1, pathbuf.0, 10, 1.0

	//GPA読み込み
	pathbuf.0 = mediadir + "\\my\\my3.gpa"
	E3DLoadGPFile hsid1, pathbuf.0, gpanimno
	E3DSetGPGroundHSID hsid1, gpanimno, hsid0
	E3DSetGPAnimNo hsid1, gpanimno

	//cam読み込み
	pathbuf.0 = mediadir + "\\my\\my3.cam"
	E3DLoadCameraFile pathbuf.0, camanimno
	E3DSetCameraAnimNo camanimno

	//MOE読み込み
	pathbuf.0 = mediadir + "\\my\\my3.moe"
	E3DLoadMOEFile hsid1, pathbuf.0, moeanimno
	E3DSetMOEAnimNo hsid1, moeanimno
	

	//ライトの作成
	lightdirx1 = 1.0
	lightdiry1 = -1.0
	lightdirz1 = 1.0
	lightr1 = 255
	lightg1 = 255
	lightb1 = 255
	E3DCreateLight lid1
	E3DSetDirectionalLight lid1, lightdirx1, lightdiry1, lightdirz1, lightr1, lightg1, lightb1
	
*main
	; keybuf変数は、メインループの外で、dim keybuf, 256 として、確保しておいてください。
	E3DGetKeyboardState keybuf

	if keybuf.VK_ESCAPE = 1 : goto *bye ; [ESC]で終了

	E3DSetNewCamera -1
	E3DSetNewGP hsid1
	E3DSetNewMOE hsid1

	E3DChkInView scid1, hsid0
	E3DChkInView scid1, hsid1

	E3DBeginScene scid1

		;不透明部分の描画をする
		E3DRender scid1, hsid0, 0, 0, 0
		E3DRender scid1, hsid1, 0, frameno1, 0

		;半透明部分の描画をする
		E3DRender scid1, hsid0, 1, 0, 0, 0, 0, 1
		E3DRender scid1, hsid1, 1, frameno1, 0, 0, 0, 1
		
	E3DEndScene
	E3DPresent scid1

	waitbyfps 60, chkfps1

	goto *main


*bye
	E3DBye
	end

